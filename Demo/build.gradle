import org.apache.commons.io.FileUtils
import org.apache.commons.io.filefilter.FileFilterUtils

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

// build.gradle (:app 최상단에 추가)
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    namespace 'com.myscript.iink.demo'

    compileSdk project.ext.compileSdk

    buildFeatures {
        viewBinding true
        buildConfig true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14" // The compatible Compose compiler version for Kotlin 1.9.24 is 1.5.14.
    }

    defaultConfig {
        minSdk project.ext.minSdk
        targetSdk project.ext.targetSdk

        applicationId 'com.myscript.iink.demo'
        versionCode 4000
        versionName '4.0.0'

        vectorDrawables.useSupportLibrary true

        buildConfigField "String", "apiKey", "\"${localProperties.getProperty('apiKey')}\""
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${project.ext.appcompatVersion}"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.activity:activity-ktx:1.10.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'

    def lifecycle_version = '2.8.6'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation project(':UIReferenceImplementation')

    // gemini
    implementation("com.google.ai.client.generativeai:generativeai:0.9.0")

    // compose
    implementation platform('androidx.compose:compose-bom:2025.02.00')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    implementation 'androidx.activity:activity-compose:1.10.0'

    // hilt
    implementation "com.google.dagger:hilt-android:2.52"
    kapt "com.google.dagger:hilt-android-compiler:2.52"

    // hilt viewmodel
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
}

task DownloadAndExtractAssets(type: Copy) {
    def sourceUrls = ['https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-diagram.zip',
                      'https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-raw-content.zip',
                      'https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-raw-content2.zip',
                      'https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-math.zip',
                      'https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-math2.zip',
                      'https://download.myscript.com/iink/recognitionAssets_iink_4.0/myscript-iink-recognition-text-en_US.zip']
    def targetDir = new File(projectDir, "src/main/assets/")
    def diagramConf = new File(targetDir, "conf/diagram.conf")
    def rawContentConf = new File(targetDir, "conf/raw-content.conf")
    def mathConf = new File(targetDir, "conf/math.conf")
    def textConf = new File(targetDir, "conf/en_US.conf")
    def rawContent2Conf = new File(targetDir, "conf/raw-content2.conf")
    def math2Conf = new File(targetDir, "conf/math2.conf")

    if (!diagramConf.exists() || !rawContentConf.exists() || !mathConf.exists() || !textConf.exists() || !rawContent2Conf.exists() || !math2Conf.exists()) {
        def tmpAssetsDir = new File(projectDir, "tmp-assets/")
        def zipDir = new File(tmpAssetsDir, "zips")

        if (!tmpAssetsDir.isDirectory())
            tmpAssetsDir.mkdirs()

        if (!zipDir.isDirectory())
            zipDir.mkdirs()

        sourceUrls.each { sourceUrl ->
            ant.get(src: sourceUrl, dest: zipDir.getPath())
        }

        File[] zipFiles = FileUtils.listFiles(zipDir, FileFilterUtils.suffixFileFilter("zip"), FileFilterUtils.trueFileFilter())
        zipFiles.each { File zipFile ->
            from zipTree(zipFile)
            into tmpAssetsDir
        }
    }
}

task CopyAssets(type: Copy, dependsOn: DownloadAndExtractAssets) {
    def targetDir = new File(projectDir, "src/main/assets/")
    def diagramConf = new File(targetDir, "conf/diagram.conf")
    def rawContentConf = new File(targetDir, "conf/raw-content.conf")
    def mathConf = new File(targetDir, "conf/math.conf")
    def textConf = new File(targetDir, "conf/en_US.conf")
    def rawContent2Conf = new File(targetDir, "conf/raw-content2.conf")
    def math2Conf = new File(targetDir, "conf/math2.conf")

    if (!diagramConf.exists() || !rawContentConf.exists() || !mathConf.exists() || !textConf.exists() || !rawContent2Conf.exists() || !math2Conf.exists()) {
        def tmpAssetsDir = new File(projectDir, "tmp-assets/")

        if (!tmpAssetsDir.isDirectory())
            tmpAssetsDir.mkdirs()

        def recognitionAssetDir = new File(tmpAssetsDir, "recognition-assets/")

        println "Copying downloaded assets from $recognitionAssetDir to $targetDir"
        from recognitionAssetDir
        into targetDir

        doLast {
            tmpAssetsDir.deleteDir()
        }
    }
}

task CopyConfigurations(type: Copy) {
    from "${projectDir}/../configurations"
    into "${projectDir}/src/main/assets/parts"
    include(
            "**/*.json"
    )
}

preBuild.dependsOn(CopyAssets, CopyConfigurations)
